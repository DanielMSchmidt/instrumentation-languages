grammar de.cau.se.instrumentation.language.ProbeLang with org.eclipse.xtext.xbase.Xbase 

generate probeLang "http://www.cau.de/se/instrumentation/language/ProbeLang"

import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase 
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	(patterns+=Pattern)*
	(probes+=Probe)*
;

Pattern:
	'pattern' name=ID '(' parameters+=Parameter (',' parameters+=Parameter)* ')' 
	(codeSequcene+=CodeElement)*
;

Parameter:
	name=ID
;

CodeElement:
	QuotedElement | ParameterRef | ToUpper
;

ParameterRef:
	ref=[Parameter|ID]
;

QuotedElement:
	value=STRING
;

Probe:
	'probe' name=ID '{'
		(replacements+=Replacement)*
	'}'
;

Replacement:
	name=ID scope=Scope '{'
		(patterns+=PatternCall)*
	'}'
;

Scope:
	'scope' modelRef=QualifiedName (insertBefore?='<')? target=Alternative (insertAfter?='>')?
;

Alternative:
	orExpr+=Sequence ('|' orExpr+=Sequence)*	
;

Sequence:
	(andExpr+=Quantifier)+
;

Quantifier:
	item=Item (type=QuantifierType)?	
;

Item:
	Operator | QuotedElement | Group
;

Operator:
	type=OperatorType
;

Group:
	'(' subTarget=Alternative ')'	
;

PatternCall:
	ref=[Pattern|ID] '(' values+=Value (',' values+=Value)* ')'
;

Value:
	(elements+=ValueElement)+
;

ValueElement:
	QuotedElement | PatternCall | ScopeRefElement 
;

ToUpper:
	'#' value=Value
;


ScopeRefElement:
	//ref=[ecore::ENamedElement|QualifiedName]
	ref=QualifiedName
;



/* */

enum QuantifierType:
	NONE_OR_MANY = '*' |
	OPTIONAL = '?' |
	ONE_OR_MANY = '+'	
;

enum OperatorType:
	ANY_CHAR = '.' |
	WHITE_SPACE = '\\s' |
	NON_WHITE_SPACE = '\\S' |
	WORD_CHAR = '\\w' |
	NON_WORD_CHAR = '\\W' |
	DIGIT = '\\d' |
	NON_DIGIT = '\\D' 
;
