grammar de.cau.se.instrumentation.language.ProbeLang with org.eclipse.xtext.xbase.Xbase 

generate probeLang "http://www.cau.de/se/instrumentation/language/ProbeLang"

import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase 
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	'package' name = QualifiedName
	(metaModels += LoadMetaModel)*
	(imports += Import)*
	(patterns+=Pattern)*
	(probes+=Probe)*
;

Import:
	'import' importedNamespace = QualifiedNameWithWildcard
;

LoadMetaModel :
	'load' name=ID ePackage=[ecore::EPackage|STRING] 
;

QualifiedNameWithWildcard:
	QualifiedName ('.' '*')?
;

Pattern:
	'pattern' name=ID ( '(' parameters+=Parameter (',' parameters+=Parameter)* ')' )? 
	(codeSequcene+=CodeElement)*
;

Parameter:
	name=ID
;

CodeElement:
	(toUpper?='#')? value=SimpleCodeElement	
;

SimpleCodeElement returns xbase::XExpression:
	XStringLiteral | ParameterRef 
;

ParameterRef:
	ref=[Parameter|ID]
;

Probe:
	'probe' name=ID ':' type=[ecore::EClassifier|ID] '{'
		(properties+=Property)*
	'}'
;

Property:
	type=[ecore::EClassifier|ID] name=ID ('{' (properties+=ReferenceProperty)* '}')?
;

ReferenceProperty:
	ref=[ecore::EReference|ID] ('{' (properties+=ReferenceProperty)* '}')? 
;

