grammar de.cau.cs.se.instrumentation.language.InstrumentationLanguage with org.eclipse.xtext.common.Terminals

generate instrumentation "http://se.cs.cau.de/languages/InstrumentationLanguage"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	'package' name = QualifiedName
	(packages += Package)*
	(imports += Import)*
	(patterns+=Pattern)*
	(records+=Record)*
;

Import:
	'import' importedNamespace = QualifiedNameWithWildcard
;

Package :
	'use' name=ID package=[ecore::EPackage|STRING] 
;

QualifiedName:
  ID (=>'.' ID)*;

QualifiedNameWithWildcard:
	QualifiedName ('.' '*')?
;

Pattern:
	'pattern' name=ID ( '(' parameters+=Parameter (',' parameters+=Parameter)* ')' )? 
	(codeSequcene+=CodeElement)*
;

Parameter:
	name=ID
;

CodeElement:
	(toUpper?='#')? value=SimpleCodeElement	
;

SimpleCodeElement:
	XStringLiteral | ParameterRef 
;

ParameterRef:
	ref=[Parameter|ID]
;

XStringLiteral:
	value=STRING
;

Record:
	'record' name=ID '{'
		(properties+=Property)*
	'}'
;

Classifier:
	package=[Package] '.' class=[ecore::EClassifier|ID]
;

Property:
	type=Classifier name=ID ('{' (properties+=ReferenceProperty)* '}')?
;

ReferenceProperty:
	ref=[ecore::EStructuralFeature|ID] ('{' (properties+=ReferenceProperty)* '}')? 
;

