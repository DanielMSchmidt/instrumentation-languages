grammar de.cau.cs.se.instrumentation.rl.RecordLang with org.eclipse.xtext.common.Terminals

generate recordLang "http://www.cau.de/cs/se/instrumentation/rl/RecordLang"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	'package' name = QualifiedName
	(packages += Package)*
	(imports += Import)*
	(records += RecordType)*
;

Import:
	'import' importedNamespace = QualifiedNameWithWildcard
;

Package:
	'use' name=ID package=[ecore::EPackage|STRING] 
;

QualifiedName:
  ID (=>'.' ID)*;

QualifiedNameWithWildcard:
	QualifiedName ('.' '*')?
;

RecordType:
	'struct' name=ID (':' parent=[RecordType|QualifiedName])? '{'
		(properties+=Property | constants+=Constant)*
	'}'
;

Constant:
	'const' type=Classifier name=ID '=' value=Literal
;

Property:
	type=Classifier name=ID 
		('{' (properties+=ReferenceProperty)* '}' | 
		'=' value=Literal)
;

Classifier:
	(package=[Package] '.')? class=[ecore::EClassifier|ID] ( '[' (sizes+=ArraySize (',' sizes+=ArraySize)*)? ']' )?
;

ArraySize:
	size=INTEGER |
	unbounded?='*'
;

ReferenceProperty:
	ref=[ecore::EStructuralFeature|ID] '{' (properties+=ReferenceProperty)* '}'
;

Literal:
	StringLiteral | IntLiteral | FloatLiteral | BooleanLiteral | ConstantLiteral
;

StringLiteral:
	value=STRING
;

IntLiteral:
	value=INTEGER
;

FloatLiteral:
	value=FLOAT
;

BooleanLiteral: 
	{BooleanLiteral} ((value?='true')|'false')
;

ConstantLiteral:
	value=[Constant|ID]
;

// terminals
terminal INTEGER returns ecore::EInt: ('+'|'-')? INT;

terminal FLOAT returns ecore::EFloat: ('+'|'-')? (INT '.' INT? | '.' INT);
